from more_itertools import unzip
from functools import partial
from pathlib import Path
from snakemake.utils import min_version, validate
from scripts.common.config import (
    input_set,
    expand_rules,
    validate_inputs,
    validate_ebm_inputs,
    validate_ebm_features,
)

min_version("6.12")


configfile: "config/static.yml"


validate(config, "schemas/config-schema.yml")
validate_inputs(config)
validate_ebm_features(config)
validate_ebm_inputs(config)


################################################################################
# Paths

conf_paths = config["paths"]
resources_dir = Path(conf_paths["resources"])
results_dir = Path(conf_paths["results"])
_scripts_dir = Path("workflow/scripts")

# this is necessary as envs/scripts are apparently resolved locally in each rule
# file
def build_abs_path(relpath, basename):
    return str(relpath.resolve() / basename)


envs_path = partial(build_abs_path, Path("workflow/envs"))
scripts_path = partial(build_abs_path, _scripts_dir)
rmd_path = partial(build_abs_path, _scripts_dir / "rmarkdown")


################################################################################
# Import Subworkflows


include: "rules/labels/parse_vcf.smk"
include: "rules/annotations/get_annotations.smk"
include: "rules/train_ebm.smk"


################################################################################
# Main Target


rule all:
    input:
        rules.all_summary.input,
        rules.all_ebm.input,


################################################################################
# Testing Targets


rule all_resources:
    input:
        expand(rules.download_input_vcf.output, input_key=[*config["inputs"]]),
        expand(rules.download_ref_sdf.output, ref_key=input_set(config, "ref")),
        expand(
            expand_rules(
                rules,
                [
                    "download_bench_vcf",
                    "download_bench_bed",
                    "generate_bench_tbi",
                ],
                "output",
            ),
            bench_key=input_set(config, "benchmark"),
        ),
        # TODO all these implicitly depend on the reference
        rules.get_genome.output,
        rules.get_mappability_high_src.output,
        rules.get_mappability_low_src.output,
        rules.get_repeat_masker_src.output,
        rules.get_superdups_src.output,
        rules.get_simreps_src.output,
